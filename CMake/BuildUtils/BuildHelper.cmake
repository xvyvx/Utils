include_guard(DIRECTORY)

include(${CMAKE_CURRENT_LIST_DIR}/../Toolchain/ToolchainHelper.cmake)
include(${CMAKE_CURRENT_LIST_DIR}/../Common/ParamHelper.cmake)

function(SetTargetPropertyWithValue target propertyName)
    if(${ARGC} EQUAL 3)
        set_target_properties(${target} PROPERTIES ${propertyName} ${ARGV2})
    endif()
endfunction()

function(SetTargetDefinitionWithValue target defType)
    foreach(defVar ${ARGN})
        list(APPEND definitionValues -D${defVar})
    endforeach()
    target_compile_definitions(${target} ${defType} ${definitionValues})
endfunction()

macro(SetTargetDefinitions target defType valueNamePrefix)
    foreach(setValueIndex RANGE 0 ${${valueNamePrefix}_index})
        if((NOT DEFINED ${valueNamePrefix}_${setValueIndex}_COND_0
                AND DEFINED ${valueNamePrefix}_${setValueIndex}_Default_0)
            OR "${valueNamePrefix}_${setValueIndex}_COND_0")
            SetTargetDefinitionWithValue(${target} ${defType} ${${valueNamePrefix}_${setValueIndex}_Default_0})
        endif()
    endforeach()
endmacro()

macro(SetTargetIncludes target incType valueNamePrefix)
    foreach(setValueIndex RANGE 0 ${${valueNamePrefix}_index})
        if((NOT DEFINED ${valueNamePrefix}_${setValueIndex}_COND_0
                AND DEFINED ${valueNamePrefix}_${setValueIndex}_Default_0)
            OR "${valueNamePrefix}_${setValueIndex}_COND_0")
            target_include_directories(${target} ${incType} ${${valueNamePrefix}_${setValueIndex}_Default_0})
        endif()
    endforeach()
endmacro()

macro(SetTargetCompileOpts target optType valueNamePrefix)
    foreach(setValueIndex RANGE 0 ${${valueNamePrefix}_index})
        if((NOT DEFINED ${valueNamePrefix}_${setValueIndex}_COND_0
                AND DEFINED ${valueNamePrefix}_${setValueIndex}_Default_0)
            OR "${valueNamePrefix}_${setValueIndex}_COND_0")
            target_compile_options(${target} ${optType} ${${valueNamePrefix}_${setValueIndex}_Default_0})
        endif()
    endforeach()
endmacro()

macro(SetTargetDeps target depType valueNamePrefix)
    foreach(setValueIndex RANGE 0 ${${valueNamePrefix}_index})
        if((NOT DEFINED ${valueNamePrefix}_${setValueIndex}_COND_0
                AND DEFINED ${valueNamePrefix}_${setValueIndex}_Default_0)
            OR "${valueNamePrefix}_${setValueIndex}_COND_0")
            target_link_libraries(${target} ${depType} ${${valueNamePrefix}_${setValueIndex}_Default_0})
        endif()
    endforeach()
endmacro()

macro(ParseAddTargetParam extRootArgKeywords)
    set(Root_Keywords "COVERAGE_BUILD" "VER" "SOVER" "SRC" "DEPS" "PRIVATE_DEFS" "PRIVATE_INC" "PRIVATE_OPTS"
        ${extRootArgKeywords})
    set(Root_DEPS_Keywords "COND")
    set(Root_PRIVATE_DEFS_Keywords "COND")
    set(Root_PRIVATE_INC_Keywords "COND")
    set(Root_PRIVATE_OPTS_Keywords "COND")
    ParseStructuredLevelParameters("Root" "Root" "${ARGN}")
endmacro()

macro(SetTargetAttribute targetName)
    if(Root_COVERAGE_BUILD_0)
        EnableCodeCoverageBuild(TARGET ${targetName})
    endif()
    EnableGNUStyleStrip(TARGET ${targetName})
    SetTargetPropertyWithValue(${targetName} VERSION ${Root_VER_0})
    SetTargetPropertyWithValue(${targetName} SOVERSION ${Root_SOVER_0})
    SetTargetDefinitions(${targetName} PUBLIC "Root_PUBLIC_DEFS")
    SetTargetDefinitions(${targetName} INTERFACE "Root_INTERFACE_DEFS")
    SetTargetDefinitions(${targetName} PRIVATE "Root_PRIVATE_DEFS")
    SetTargetIncludes(${targetName} PUBLIC "Root_PUBLIC_INC")
    SetTargetIncludes(${targetName} INTERFACE "Root_INTERFACE_INC")
    SetTargetIncludes(${targetName} PRIVATE "Root_PRIVATE_INC")
    SetTargetCompileOpts(${targetName} PUBLIC "Root_PUBLIC_OPTS")
    SetTargetCompileOpts(${targetName} INTERFACE "Root_INTERFACE_OPTS")
    SetTargetCompileOpts(${targetName} PRIVATE "Root_PRIVATE_OPTS")
    SetTargetDeps(${targetName} PUBLIC "Root_DEPS")
    target_compile_features(${targetName} PUBLIC cxx_std_14)
    if(UNIX)
        target_compile_options(${targetName} PRIVATE -Wall) 
    elseif(WIN32)
        target_compile_options(${targetName} PRIVATE "/source-charset:utf-8")
        target_compile_definitions(${targetName} PUBLIC -D_WIN32_WINNT=0x0600 PRIVATE -D_SCL_SECURE_NO_WARNINGS)
        target_compile_options(${targetName} PRIVATE $<$<OR:$<CONFIG:RELEASE>,$<CONFIG:MINSIZEREL>,$<CONFIG:RELWITHDEBINFO>>:/GL>)
        set_property(TARGET ${targetName} APPEND PROPERTY LINK_FLAGS_RELEASE " /LTCG /OPT:REF")
        set_property(TARGET ${targetName} PROPERTY LINK_FLAGS_MINSIZEREL " /LTCG /OPT:REF")
        set_property(TARGET ${targetName} PROPERTY LINK_FLAGS_RELWITHDEBINFO " /LTCG /OPT:REF")
    else()
        message(FATAL_ERROR "Unsupported platform.")
    endif()
endmacro()

function(AddExecutableTarget targetName)
    ParseAddTargetParam("" ${ARGN})
    add_executable(${targetName} ${Root_SRC_0})
    SetTargetAttribute(${targetName})
endfunction()

function(AddSharedLibraryTarget targetName)
    set(extRootKeywords "PUBLIC_DEFS;INTERFACE_DEFS;PUBLIC_INC;INTERFACE_INC;PUBLIC_OPTS;INTERFACE_OPTS")
    set(Root_PUBLIC_DEFS_Keywords "COND")
    set(Root_INTERFACE_DEFS_Keywords "COND")
    set(Root_PUBLIC_INC_Keywords "COND")
    set(Root_INTERFACE_INC_Keywords "COND")
    set(Root_PUBLIC_OPTS_Keywords "COND")
    set(Root_INTERFACE_OPTS_Keywords "COND")
    ParseAddTargetParam("${extRootKeywords}" ${ARGN})
    add_library(${targetName} SHARED ${Root_SRC_0})
    string(TOUPPER ${targetName} upperTargetName)
    set_target_properties(${targetName} PROPERTIES CXX_VISIBILITY_PRESET hidden VISIBILITY_INLINES_HIDDEN ON
        DEFINE_SYMBOL "${upperTargetName}_EXPORTS")
    target_compile_definitions(${targetName} INTERFACE "${upperTargetName}_IMPORTS")
    SetTargetAttribute(${targetName} addShrLib)
endfunction()